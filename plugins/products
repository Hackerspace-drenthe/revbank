#!perl

HELP "<productID>" => "Look up products from database";
HELP "edit" => "Edit product list";

my $filename = 'revbank.products';

sub command :Tab(edit) {
    my ($self, $cart, $command) = @_;

    if ($command eq 'edit') {
        system $ENV{EDITOR} || 'vi', $filename;
        return ACCEPT;
    }

    my @products;

    open my $fh, '<', $filename or die $!;
    /\S/ && !/^\s*#/ and push @products, [split " ", $_, 3] while readline $fh;
    chomp @$_ for @products;

    for my $fields (@products) {
        my ($id, $price, $description) = @$fields;

        next if $command ne $id;

        $price = parse_amount($price) or next;

        $cart->add(undef, -$price, $description);
        return ACCEPT;
    }

    return NEXT;
}
